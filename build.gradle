plugins {
    id "org.springframework.boot" version "2.5.9" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id 'org.unbroken-dome.test-sets' version '4.0.0' apply false
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.0.0' apply false
}

allprojects {
    group "by.milosh"
    version "0.0.1"

    ext {
        checkstyleToolVersion = "8.41"
        mapStructVersion = "1.4.2.Final"
        springfoxSwaggerVersion = "3.0.0"
        lombokVersion = "1.18.22"
        lombokMapstructBindingVersion = "0.2.0"
        flywayVersion = "8.4.4"
        pgEmbeddedVersion = "0.13.4"
    }

    configure(subprojects) {
        apply plugin: "java"
        apply plugin: 'checkstyle'
        apply plugin: 'jacoco'

        sourceCompatibility = JavaVersion.VERSION_15
        targetCompatibility = JavaVersion.VERSION_15

        repositories {
            mavenCentral()
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        dependencies {
            implementation "io.springfox:springfox-boot-starter:$springfoxSwaggerVersion"
            compileOnly "org.projectlombok:lombok:$lombokVersion"
            testCompileOnly "org.projectlombok:lombok:$lombokVersion"
            annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
            implementation "org.projectlombok:lombok-mapstruct-binding:$lombokMapstructBindingVersion"
            implementation "org.mapstruct:mapstruct:$mapStructVersion"
            annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"
        }

        checkstyle {
            toolVersion "$checkstyleToolVersion"
            configFile rootProject.file("config/checkstyle.xml")
        }

        test {
            useJUnitPlatform()
        }

        jacoco {
            toolVersion = '0.8.6'
        }

        def execFiles = project.fileTree(project.buildDir).include("/jacoco/*.exec")
        def path = 'by/milosh'
        def excludedTests = [
                path + '/controller/**'
        ]

        jacocoTestReport.dependsOn test
        jacocoTestReport {
            afterEvaluate {
                classDirectories.setFrom(files(classDirectories.files.collect {
                    fileTree(dir: it, exclude:
                            excludedTests
                    )
                }))
            }
        }

        jacocoTestCoverageVerification.dependsOn jacocoTestReport

        jacocoTestCoverageVerification {
            violationRules {
                rule {
                    limit {
                        counter = 'LINE'
                        minimum = 0.5
                    }
                }
            }
        }

        //check.dependsOn jacocoTestCoverageVerification
    }
}
